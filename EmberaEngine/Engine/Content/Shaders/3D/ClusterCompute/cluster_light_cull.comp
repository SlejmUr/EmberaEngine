#version 440 core

layout(local_size_x = 16, local_size_y = 9, local_size_z = 4) in;

struct LightGrid {
	uint offset;
	uint count;
};

struct PointLight {
	vec4 position;
    vec4 color;
    bool enabled;
    float intensity;
    float range;
};

struct Cluster
{
    vec4 minPoint;
    vec4 maxPoint;
    uint count;
    uint lightIndices[100];
};

layout (std430, binding = 0) buffer pointLightSSBO {
    PointLight pointLights[];
};

layout (std430, binding = 1) buffer lightGridSSBO {
    LightGrid lightGrids[];
};

layout(std430, binding = 2) restrict buffer clusterSSBO {
    Cluster clusters[];
};

layout(std430, binding = 3) buffer globalIndexCountSSBO {
    uint globalIndexCount;
};

layout(std430, binding = 4) buffer globalLightIndexListSSBO {
    uint globalLightIndexList[];
};

shared PointLight sharedLights[16 * 4 * 9];

uniform mat4 W_VIEW_MATRIX;

bool testSphereAABB(uint light, uint tile);
float sqDistPointAABB(vec3 point, uint tile);


void main() {
    globalIndexCount = 0;
    uint threadCount = gl_WorkGroupSize.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z;
    uint lightCount = pointLights.length();
    uint numBatches = (lightCount + threadCount - 1) / threadCount;

    uint tileIndex = gl_LocalInvocationIndex + gl_WorkGroupSize.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z * gl_WorkGroupID.z;

    uint visibleLightCount = 0;
    uint visibleLightIndices[100];

    for (uint batch = 0; batch < numBatches; ++batch) {
        uint lightIndex = batch * lightCount + gl_LocalInvocationIndex;

        lightIndex = min(lightIndex, lightCount);

        sharedLights[gl_LocalInvocationIndex] = pointLights[lightIndex];
        barrier();

        for (uint light = 0; light < threadCount; ++light) {
            if (sharedLights[light].enabled == true) {
                if (testSphereAABB(light, tileIndex)) {
                    visibleLightIndices[visibleLightCount] = batch * threadCount + light;
                    visibleLightCount += 1;
                }
            }
        }
    }
    barrier();

    uint offset = atomicAdd(globalIndexCount, visibleLightCount);

    for(uint i = 0; i < visibleLightCount; ++i){
        globalLightIndexList[offset + i] = visibleLightIndices[i];
    }

    lightGrids[tileIndex].offset = offset;
    lightGrids[tileIndex].count = visibleLightCount;

}

bool testSphereAABB(uint light, uint tile){
    float radius = sharedLights[light].range;
    vec3 center  = vec3(W_VIEW_MATRIX * sharedLights[light].position);
    float squaredDistance = sqDistPointAABB(center, tile);

    return squaredDistance <= (radius * radius);
}

float sqDistPointAABB(vec3 point, uint tile){
    float sqDist = 0.0;
    Cluster currentCell = clusters[tile];
    clusters[tile].maxPoint[3] = tile;
    for(int i = 0; i < 3; ++i){
        float v = point[i];
        if(v < currentCell.minPoint[i]){
            sqDist += (currentCell.minPoint[i] - v) * (currentCell.minPoint[i] - v);
        }
        if(v > currentCell.maxPoint[i]){
            sqDist += (v - currentCell.maxPoint[i]) * (v - currentCell.maxPoint[i]);
        }
    }

    return sqDist;
}